// =====================================================
// Copyright 2025 Focason Co.,Ltd. AllRights Reserved.
// =====================================================
import java.nio.charset.StandardCharsets

buildscript {
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:8.0.0'
    }
}

plugins {
    id 'java'
}

apply plugin: 'com.diffplug.spotless'

allprojects {
    group = 'com.focason'
    version = '1.0.0-SNAPSHOT'
    compileJava.options.encoding = StandardCharsets.UTF_8
    apply plugin: 'java'
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    ext {
        set('springCloudVersion', '2023.0.0') // Spring Cloud 2023.0.0 (Leyton) for Spring Boot 3.2.x
        set('springBootVersion', '3.2.0') // Spring Boot 3.2.0
        set('springDependencyVersion', '1.1.3') // Updated for Spring Boot 3.x
        set('springCloudAwsVersion', '3.1.0') // Spring Cloud AWS 3.1.0
    }

    repositories {
        mavenCentral()
        maven { url = 'https://artifactory-oss.prod.netflix.net/artifactory/maven-oss-candidates' }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/generated/main/java'
                srcDirs 'src/main/java'
                srcDirs 'src/test/java'
            }
            resources {
                srcDirs 'src/generated/main/resources'
                srcDirs 'src/main/resources'
                srcDirs 'src/test/resources'
            }
        }
    }

    // 在构建前运行 spotlessCheck，且不触发 cleanGen
    tasks.named("build") {
        dependsOn("spotlessApply")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    configurations {
        domaGenRuntime
    }

    dependencies {
        // Spring Boot
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // lombok
        compileOnly 'org.projectlombok:lombok:1.18.26'
        annotationProcessor 'org.projectlombok:lombok:1.18.26'

        // MySQL
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'
        runtimeOnly 'mysql:mysql-connector-java:8.0.33'

        // Doma2
        implementation 'org.seasar.doma:doma-core:2.53.0'
        implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.6.0'
        domaGenRuntime 'mysql:mysql-connector-java:8.0.33'
        domaGenRuntime 'org.seasar.doma:doma-gen:2.28.0'
        annotationProcessor 'org.seasar.doma:doma-processor:2.53.0'

        // JsonObject
        implementation 'com.google.code.gson:gson:2.10.1'
    }

    // Add spotless into all projects.
    spotless {
        java {
            importOrder()
            eclipse().configFile file("${rootDir}/eclipse-java-code-formatter.xml")
            removeUnusedImports()
        }
    }

    // 【DOMA4019】のため
    tasks.register('copyDomaResources', Sync) {
        from sourceSets.main.resources.srcDirs
        into compileJava.destinationDirectory
        include 'doma.compile.config'
        include 'META-INF/**/*.sql'
        include 'META-INF/**/*.script'
    }

    // JavaクラスとSQLファイルの出力先ディレクトリを同じにする
    processResources {
        compileJava.destinationDirectory
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

    compileJava {
        // Depend on the above task
        dependsOn processResources
        dependsOn copyDomaResources
        options.encoding = 'UTF-8'
    }

    tasks.withType(Copy).configureEach { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

    tasks.withType(Jar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

