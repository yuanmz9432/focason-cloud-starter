// =====================================================
// Copyright 2025 Focason Co.,Ltd. AllRights Reserved.
// =====================================================

buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:6.2.2'
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyVersion}"
}
apply plugin: 'org.flywaydb.flyway'

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'
    // JWT (updated for Spring Boot 3.x)
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    // Apache POI for Excel (XLSX) handling
    implementation 'org.apache.poi:poi-ooxml:5.2.5' // Updated for Spring Boot 3.x compatibility
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

flyway {
    url = "jdbc:mysql://${DB_HOST}:${DB_PORT}/${DB_SCHEMA}?useSSL=false&autoReconnect=true&characterEncoding=UTF-8&useUnicode=yes"
    user = "${DB_USERNAME}"
    password = "${DB_PASSWORD}"
    locations = ["filesystem:$projectDir/database/ddl"]
}

//tasks.register('cleanGen') {
//    group = 'doma-gen'
//    delete 'src/generated'
//}

tasks.register('domaGen') {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties', classpath: configurations.domaGenRuntime.asPath)
        ant.gen(
                url: "jdbc:mysql://${DB_HOST}:${DB_PORT}/${DB_SCHEMA}?useSSL=false&useInformationSchema=false&characterEncoding=UTF-8&useUnicode=yes&allowPublicKeyRetrieval=true&serverTimezone=Asia/Tokyo",
                user: "${DB_USERNAME}",
                password: "${DB_PASSWORD}",
                ignoredTableNamePattern: "flyway_schema_history|.*Summary.*|.*By.*|sys_.*|performance_schema\\..*|information_schema\\..*|mysql\\..*",
                templatePrimaryDir: "../focason-core/doma/template",
                tableTypes: "TABLE, VIEW")
                {
                    entityConfig(
                            overwrite: true,
                            useListener: false,
                            showCatalogName: false,
                            packageName: "com.focason.core.entity",
                            entitySuffix: "Entity",
                            destDir: 'src/generated/main/java'
                    )
                    daoConfig(
                            overwrite: true,
                            packageName: "com.focason.core.dao",
                            suffix: "Dao",
                            destDir: 'src/generated/main/java')
                    sqlConfig(
                            generate: true,
                            overwrite: true,
                            destDir: 'src/generated/main/resources',
                    )
                }
    }
}
